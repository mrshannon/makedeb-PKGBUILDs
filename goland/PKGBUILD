# Maintainer: Michael R. Shannon <mrshannon.aerospace@gmail.com>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>

nolint=1
_pkgname=GoLand
pkgname=$(echo "$_pkgname" | tr '[:upper:]' '[:lower:]')
pkgver=2019.3.4
pkgrel=1
section='devel'
pkgdesc='Capable and Ergonomic Go IDE'
read -r -d '' pkglong <<"EOF"
GoLand is a commercial IDE by JetBrains aimed at providing an ergonomic
environment for Go development. GoLand extends the IntelliJ platform with the
coding assistance and tool integrations specific for the Go language.
EOF
arch=('amd64')
url='https://www.jetbrains.com/go/'
license=('Commercial')
source=("https://download.jetbrains.com/go/${pkgname}-${pkgver}.tar.gz"
        "jetbrains-goland.desktop")
sha256sums=('0b6ce1a51e405f4b6c31da670a52d802d298bd6d95995f57a846117b2f99b105'
            '2f1ffecf1811b3c9ae52fae5ec9ecc17e828aace6f044d65cdeb35d75e4e38d1')

prepare() {
  # remove other architectures
  rm -rf "${srcdir}/${_pkgname}-${pkgver}/bin/fsnotifier"
  rm -rf "${srcdir}/${_pkgname}-${pkgver}/bin/fsnotifier-arm"
  rm -rf "${srcdir}/${_pkgname}-${pkgver}/lib/libpty/linux/x86"
}

package() {

    # install goland
    install -dm755 "${pkgdir}/opt/jetbrains/${pkgname}"
    tar -C "${srcdir}/${_pkgname}-${pkgver}" -cf - \
        . | tar -C "${pkgdir}/opt/jetbrains/${pkgname}" -xf -

    # install binary
    install -dm755 "${pkgdir}/usr/bin/"
    ln -s "/opt/jetbrains/${pkgname}/bin/${pkgname}.sh" \
            "${pkgdir}/usr/bin/${pkgname}"

    # install .desktop
    install -Dm644 "${srcdir}/jetbrains-${pkgname}.desktop" \
            "${pkgdir}/usr/share/applications/jetbrains-${pkgname}.desktop"

    # install icon
    install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/bin/${pkgname}.png" \
            "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

    # install docs
    install -dm755 "${docdir}"
    tar -C "${srcdir}/${_pkgname}-${pkgver}/license/" -cf - \
        . | tar -C "${docdir}/" -xf -
}
