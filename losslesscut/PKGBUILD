# Maintainer: Michael R. Shannon <mrshannon.aerospace@gmail.com>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=losslesscut
pkgver=2.1.0
pkgrel=1
section='video'
pkgdesc="Cross platform GUI tool for lossless trimming/cutting of video/audio files"
read -r -d '' pkglong <<"EOF"
Simple, cross platform tool for lossless trimming/cutting of video and audio
files. Great for rough processing of large video files taken from a video
camera, GoPro, drone, etc. It lets you quickly extract the good parts from
your videos and discard GBs of data without losing quality. It doesn't do
any decoding / encoding and is therefore extremely fast.
EOF
arch=('amd64')
url="https://github.com/mifi/lossless-cut"
license=('MIT')
depends=(
    'ffmpeg'
    'libasound2'
    'libatk1.0-0'
    'libc6'
    'libcairo2'
    'libcups2'
    'libdbus-1-3'
    'libexpat1'
    'libfontconfig1'
    'libgcc1'
    'libgconf-2-4'
    'libgdk-pixbuf2.0-0'
    'libglib2.0-0'
    'libgtk-3-0'
    'libnspr4'
    'libnss3'
    'libpango-1.0-0'
    'libpangocairo-1.0-0'
    'libstdc++6'
    'libx11-6'
    'libx11-xcb1'
    'libxcb1'
    'libxcomposite1'
    'libxcursor1'
    'libxdamage1'
    'libxext6'
    'libxfixes3'
    'libxi6'
    'libxrandr2'
    'libxrender1'
    'libxss1'
    'libxtst6')
makedepends=()
source=("https://github.com/mifi/lossless-cut/releases/download/v${pkgver}/LosslessCut-linux-x64.zip" 
        'losslesscut.desktop'
        'icon.svg')
sha256sums=('5b6fbeecccbc3ed780ed828376e8dca14507770f9f5e94c9e675e0dc42da19be'
            'e91017634fe1e3685a6affd8d72c1ec11af4d390c9fc13d503ae67e8c6e4e852'
            'd3d3da3f403ce1b9f846ae2a38a8fe9938fc458024352a9741b59a920eefacf9')
package()
{
    # install docs
    install -dm755 "$docdir"
    install -Dm644 "${srcdir}/LosslessCut-linux-x64/LICENSE" "${docdir}/LICENSE"
    install -Dm644 "${srcdir}/LosslessCut-linux-x64/LICENSES.chromium.html" \
        "${docdir}/LICENSES.chromium.html"

    # install application
    install -dm755 "${pkgdir}/usr/share/${pkgname}"
    tar -C "${srcdir}/LosslessCut-linux-x64" -cf - \
        --exclude app.asar.unpacked \
        --exclude LICENSE \
        --exclude LICENSES.chromium.html \
        . | tar -C "${pkgdir}/usr/share/${pkgname}" -xf -
    chmod a-x "${pkgdir}/usr/share/${pkgname}/libffmpeg.so"
    chmod a-x "${pkgdir}/usr/share/${pkgname}/libnode.so"

    # install binary
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "../share/${pkgname}/LosslessCut" "${pkgdir}/usr/bin/${pkgname}"

    # install icon, extracted from LosslessCut v1.12.0 using asar
    # package function does not extract it because asar is a snap package
    install -Dm644 "${srcdir}/icon.svg" "$pkgdir/usr/share/pixmaps/${pkgname}.svg"

    # install .desktop
    install -Dm644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  }
