# Maintainer: Michael R. Shannon <mrshannon.aerospace@gmail.com>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>

nolint=1
pkgname=pycharm
pkgver=2019.1.3
pkgrel=1
section='devel'
pkgdesc="Python IDE for Professional Developers"
read -r -d '' pkglong <<"EOF"
PyCharm Professional Edition is an IDE for professional Python development. It
is designed by programmers, for programmers, to provide all the tools you need
for productive Python, Web and Scientific development.
EOF
arch=('amd64')
url='http://www.jetbrains.com/pycharm/'
conflicts=('pycharm-community')
provides=('pycharm')
license=('custom')
depends=('gcc' 'libgif7' 'libc6' 'bash' 'libxext6' 'libxtst6')
makedepends=('python-setuptools' 'python3-setuptools')
optdepends=('ipython'
            'ipython3'
            'python-setuptools'
            'python3-setuptools'
            'python-coverage'
            'python3-coverage'
            'cython'
            'cython3'
            'python-pytest'
            'python3-pytest'
            'python-tox'
            'python3-tox'
            'jupyter')
source=("https://download.jetbrains.com/python/${pkgname}-professional-${pkgver}.tar.gz"
        'jetbrains-pycharm.desktop')
sha256sums=('8322de8b816895fcb7fd854e83d25084b0593ea2b90b5b4598a495ef2e958824'
            'bf2250e1ce06cddb8deec31a51e8197c87adbf62462318cd288077554c39fa09')

prepare() {
  # remove other architectures
  rm -rf "${srcdir}/${pkgname}-${pkgver}/bin/fsnotifier"
  rm -rf "${srcdir}/${pkgname}-${pkgver}/bin/fsnotifier-arm"
  rm -rf "${srcdir}/${pkgname}-${pkgver}/lib/libpty/linux/x86"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # compile PyDev debugger used by PyCharm to speedup debugging
  python2 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
  python3 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
}

package() {

    # install pycharm
    install -dm755 "${pkgdir}/opt/jetbrains/${pkgname}"
    tar -C "${srcdir}/${pkgname}-${pkgver}" -cf - \
        . | tar -C "${pkgdir}/opt/jetbrains/${pkgname}" -xf -

    # install binary
    install -dm755 "${pkgdir}/usr/bin/"
    ln -s "/opt/jetbrains/${pkgname}/bin/pycharm.sh" "${pkgdir}/usr/bin/${pkgname}"

    # install .desktop
    install -Dm644 "${srcdir}/jetbrains-pycharm.desktop" \
            "${pkgdir}/usr/share/applications/jetbrains-pycharm.desktop"

    # install icon
    install -Dm644 "${srcdir}/${pkgname}-${pkgver}/bin/pycharm.png" \
            "${pkgdir}/usr/share/pixmaps/pycharm.png"

    # install docs
    install -dm755 "${docdir}"
    tar -C "${srcdir}/${pkgname}-${pkgver}/license/" -cf - \
        . | tar -C "${docdir}/" -xf -
}
