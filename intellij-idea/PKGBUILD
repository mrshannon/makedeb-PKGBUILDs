# Maintainer: Michael R. Shannon <mrshannon.aerospace@gmail.com>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

nolint=1
pkgname=intellij-idea
pkgver=2019.3.1
_buildver=193.5233.102
pkgrel=1
arch=('amd64')
section='devel'
pkgdesc="Capable & Ergonomic Java IDE for Enterprise, Web & Mobile Development"
read -r -d '' pkglong <<"EOF"
IntelliJ IDEA Ultimate is a fully-fledged commercial IDE for the JVM platform.
IntelliJ IDEA provides all the tools you need for productive enterprise, web,
and mobile development. IntelliJ IDEA supports Java, Groovy, Kotlin, Scala,
Android, JavaScript, SQL and lots of other languages and frameworks. It offers
instant and clever code completion, on-the-fly code analysis and reliable
refactoring tools. Mission-critical tools such as integrated version controls
systems and a wide variety of supported languages and frameworks are at hand â€”
no plugin hustle included.
EOF
url="https://www.jetbrains.com/idea/"
license=('Commercial')
depends=('default-jre | java-runtime')
source=("https://download.jetbrains.com/idea/ideaIU-${pkgver}.tar.gz"
        "jetbrains-idea.desktop")
sha256sums=('87543537c524c6f67c88e1e2af3865bb233099bf405db2df131a66ebf4655532'
            'c0fa5e94568fae54b07a1e57be5d7faf428a81cde02fd2a5cf72676223078967')

prepare() {
  # remove other architectures
  rm -rf "${srcdir}/idea-IU-${_buildver}/bin/fsnotifier"
  rm -rf "${srcdir}/idea-IU-${_buildver}/bin/fsnotifier-arm"
  rm -rf "${srcdir}/idea-IU-${_buildver}/lib/libpty/linux/x86"
}

package() {

    # install idea
    install -dm755 "${pkgdir}/opt/jetbrains/${pkgname}"
    tar -C "${srcdir}/idea-IU-${_buildver}" -cf - \
        . | tar -C "${pkgdir}/opt/jetbrains/${pkgname}" -xf -

    # install binary
    install -dm755 "${pkgdir}/usr/bin/"
    ln -s "/opt/jetbrains/${pkgname}/bin/idea.sh" \
            "${pkgdir}/usr/bin/${pkgname}"

    # install .desktop
    install -Dm644 "${srcdir}/jetbrains-idea.desktop" \
            "${pkgdir}/usr/share/applications/jetbrains-idea.desktop"

    # install icon
    install -Dm644 "${srcdir}/idea-IU-${_buildver}/bin/idea.png" \
            "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

    # install docs
    install -dm755 "${docdir}"
    tar -C "${srcdir}/idea-IU-${_buildver}/license/" -cf - \
        . | tar -C "${docdir}/" -xf -
}
